/mob/living/simple_animal/hostile/ordeal/indigo_noon
	name = "sweeper"
	desc = "A humanoid creature wearing metallic armor. It has bloodied hooks in its hands."
	icon = 'ModularLobotomy/_Lobotomyicons/tegumobs.dmi'
	icon_state = "sweeper_1"
	icon_living = "sweeper_1"
	icon_dead = "sweeper_dead"
	faction = list("indigo_ordeal")
	maxHealth = 500
	health = 500
	move_to_delay = 4
	stat_attack = DEAD
	melee_damage_type = BLACK_DAMAGE
	melee_damage_lower = 20
	melee_damage_upper = 24
	butcher_results = list(/obj/item/food/meat/slab/sweeper = 2)
	guaranteed_butcher_results = list(/obj/item/food/meat/slab/sweeper = 1)
	attack_verb_continuous = "stabs"
	attack_verb_simple = "stab"
	attack_sound = 'sound/effects/ordeals/indigo/stab_1.ogg'
	damage_coeff = list(RED_DAMAGE = 1, WHITE_DAMAGE = 1.5, BLACK_DAMAGE = 0.5, PALE_DAMAGE = 0.8)
	blood_volume = BLOOD_VOLUME_NORMAL
	silk_results = list(/obj/item/stack/sheet/silk/indigo_advanced = 1,
						/obj/item/stack/sheet/silk/indigo_simple = 2)
	/// If this is FALSE, we don't get to eat human corpses, they should be saved for the Matriarch.
	var/permitted_to_feast = TRUE

/mob/living/simple_animal/hostile/ordeal/indigo_noon/Initialize()
	. = ..()
	attack_sound = "sound/effects/ordeals/indigo/stab_[pick(1,2)].ogg"
	icon_living = "sweeper_[pick(1,2)]"
	icon_state = icon_living

/mob/living/simple_animal/hostile/ordeal/indigo_noon/Aggro()
	. = ..()
	a_intent_change(INTENT_HARM)

/mob/living/simple_animal/hostile/ordeal/indigo_noon/LoseAggro()
	. = ..()
	a_intent_change(INTENT_HELP)

/mob/living/simple_animal/hostile/ordeal/indigo_noon/AttackingTarget(atom/attacked_target)
	. = ..()
	if(. && isliving(attacked_target))
		var/mob/living/L = attacked_target
		if(!permitted_to_feast && ishuman(L)) // We do not get to activate Devour on human corpses if the Matriarch wants the corpse for herself.
			return

		if(L.stat != DEAD)
			if(L.health <= HEALTH_THRESHOLD_DEAD && HAS_TRAIT(L, TRAIT_NODEATH))
				SweeperDevour(L)
		else
			SweeperDevour(L)

/mob/living/simple_animal/hostile/ordeal/indigo_noon/PickTarget(list/Targets)
	if(permitted_to_feast && health <= maxHealth * 0.6) // If we're damaged enough
		for(var/mob/living/simple_animal/hostile/ordeal/indigo_noon/sweeper in ohearers(7, src)) // And there is no sweepers even more damaged than us
			if(sweeper.stat != DEAD && (health > sweeper.health))
				return ..()
		var/list/highest_priority = list()
		for(var/mob/living/L in Targets)
			if(!CanAttack(L))
				continue
			if(ishuman(L))
				continue
			if(L.health < 0 || L.stat == DEAD)
				highest_priority += L
		if(LAZYLEN(highest_priority))
			return pick(highest_priority)
	var/list/lower_priority = list() // We aren't exactly damaged, but it'd be a good idea to finish the wounded first
	for(var/mob/living/L in Targets)
		if(!CanAttack(L))
			continue
		if(ishuman(L))
			continue
		if(L.health < L.maxHealth*0.5 && (L.stat < UNCONSCIOUS))
			lower_priority += L
	if(LAZYLEN(lower_priority))
		return pick(lower_priority)
	return ..()

/// This may or may not be expensive but it's the most orderly way I could think of to allow them to eat dead sweeper corpses.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/PossibleThreats(max_range, consider_attack_condition)
	. = ..()
	if(health <= maxHealth * 0.8)
		for(var/turf/adjacent_turf in orange(1, src))
			for(var/mob/maybe_sweeper_corpse in adjacent_turf)
				if(faction_check_mob(maybe_sweeper_corpse) && maybe_sweeper_corpse.stat == DEAD)
					. |= maybe_sweeper_corpse

/// As of June 2025 Indigo Noon is being updated to have some variants.
/// These two subtypes will show up alongside the normal old sweepers for the ordeal. They could also be reused in Dusk and Midnight.

/// This subtype moves faster, attacks faster, deals less damage per hit, and has access to a dash attack.
/// Uses the lanky sweeper sprite made by insiteparaful.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky
	health = 300
	maxHealth = 300
	icon = 'ModularLobotomy/_Lobotomyicons/32x48.dmi'
	icon_state = "sweeper_limbus"
	icon_living = "sweeper_limbus"
	desc = "A humanoid creature wearing metallic armor. It has bloodied hooks in its hands.\nThis one seems to move with far more agility than its peers."
	move_to_delay = 2.7
	/// The default movement speed this Sweeper should have. I have to store it here because initial() won't work for my purposes,
	/// since it gets the compiletime value.
	// I am aware this looks bad, forgive me.
	var/movespeed = 2.7
	rapid_melee = 2
	melee_damage_lower = 11
	melee_damage_upper = 13
	simple_mob_flags = SILENCE_RANGED_MESSAGE
	/// This shouldn't matter until it goes into evasive mode.
	dodge_prob = 40
	/// I want it to be ranged so it'll use OpenFire() on targets it's not in melee with, which I am overriding with an attempt to use the dash attack. That being said it isn't a real ranged unit.
	ranged = TRUE
	projectiletype = null
	/// Placeholder here until the main PR for can_act and can_move is merged.
	var/can_act = TRUE
	/// Holds the next moment that this mob will be allowed to dash.
	var/dash_cooldown
	/// This is the amount of time added by its dash attack (Sweep the Backstreets) on use onto its cooldown.
	// Reduced by hitting enemies with it.
	var/dash_cooldown_time = 8 SECONDS
	/// Sweep the Backstreets ability range in tiles.
	var/dash_range = 3
	/// Sweep the Backstreets healing per human hit.
	var/dash_healing = 80
	/// Are we currently during the dash windup phase?
	var/dash_preparing = FALSE
	/// Are we currently dashing?
	var/dash_dashing = FALSE
	/// The speed at which we dash in deciseconds.
	var/dash_speed = 0.4
	/// The windup duration for the dash.
	var/dash_windup = 0.7 SECONDS
	/// Duration of Evasive Mode after dashing.
	var/dash_evasivemode_duration = 3 SECONDS
	/// The buffed movement speed this Sweeper has during Evasive Mode, if it has no client.
	var/dash_evasivemode_noclient_speed = 2.2
	/// The buffed movement speed this Sweeper has during Evasive Mode if it has a client.
	var/dash_evasivemode_client_speed = 2.4
	/// We need this to not hit multiple people due to the implementation I used for the dash. Stores every mob hit by the dash, cleared on each dash.
	var/list/dash_hitlist = list()
	/// This one is so we can hit all the turfs with the dash at once, to avoid people dodging it by moving inside of it.
	var/list/dash_hitlist_turfs = list()

	// CoL Adjustments: Change these to nerf/buff this variant on City maps, on Initialize.
	// At the moment I've left them all at 0, because I do not think I need to nerf them on CoL.
	/// ADDS TO dash_cooldown_time on CoL. If this is positive, dash has a longer cooldown, if it is negative, it is shortened.
	var/COL_dash_cooldown_adjustment = 0
	/// ADDS TO dash_evasivemode_duration on CoL. If this is positive, Evasive Mode lasts longer, if it is negative, it is shortened.
	var/COL_dash_evasivemode_duration_adjustment = 0
	/// ADDS TO dash_evasivemode_client_speed and dash_evasivemode_noclient_speed on CoL. If this is positive, it is less of a speed boost, negative is viceversa.
	var/COL_dash_evasivemode_speed_adjustment = 0

/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/Initialize()
	. = ..()
	/// I know this is weird but I don't know how to ONLY override Initialize() for indigo_noon without getting rid of the code from simple_animal and whatnot.
	/// I have to set these things here because normal indigo_noon initialization sets them.
	icon_living = "sweeper_limbus"
	icon_state = icon_living
	attack_sound = 'sound/effects/ordeals/indigo/stab_2.ogg'

	/// COL Rebalancing
	if(SSmaptype.maptype in SSmaptype.citymaps)
		dash_cooldown_time += COL_dash_cooldown_adjustment
		dash_evasivemode_duration += COL_dash_evasivemode_duration_adjustment
		dash_evasivemode_client_speed += COL_dash_evasivemode_speed_adjustment
		dash_evasivemode_noclient_speed += COL_dash_evasivemode_speed_adjustment

/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/Destroy()
	/// To avoid a hard delete.
	dash_hitlist = null
	dash_hitlist_turfs = null
	. = ..()

/// When meleeing a target, will attempt to dash if it's available (and has some RNG thrown into it to keep them less predictable). Won't dash on melee if it's a possessed sweeper.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/AttackingTarget(atom/attacked_target)
	if(!can_act)
		return FALSE
	if(dash_cooldown > world.time || dash_dashing || dash_preparing)
		return ..()
	if(!client && prob(60))
		var/mob/living/victim = attacked_target
		if(istype(victim) && victim.stat != DEAD)
			SweepTheBackstreets(victim)
			return
	. = ..()

/// OpenFire() is gonna be called fairly often since it's set as a ranged unit, we want this so they'll dash even if they're stuck behind other sweepers in a "traffic jam". Also lets possessed sweepers dash at will.
/// Whole proc is overridden.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/OpenFire(atom/A)
	if(dash_cooldown > world.time || dash_dashing || dash_preparing)
		return
	if(client)
		SweepTheBackstreets(A)
		return
	else if(prob(50))
		SweepTheBackstreets(A)
		return

/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/Move(atom/newloc, dir, step_x, step_y)
	if(dash_preparing)
		return FALSE
	. = ..()
	if(!.)
		CancelDash()
	/// While we're dashing, all turfs we move into will be added into the list of turfs hit by our dash. Final turf is added manually in SweepTheBackstreets().
	if(dash_dashing)
		dash_hitlist_turfs |= get_turf(newloc)

/// Dash attack. Calls PrepareDash(), BeginDash(), CancelDash() and SweepTheBackstreetsHit() in that order.
/// Sequence of events:
/// 1. Checks passed, sweeper goes on cooldown, target turf is telegraphed and the sweeper says something to warn players. Sweeper then sleeps for var/dash_windup.
/// 2. Sweeper begins dashing, walking towards the target turf. Slept for the duration of the movement, and it can't move or act normally during this.
/// 3. After the movement is over, a red-coloured beam is drawn between the starting point and the sweeper, and a sound is played. All turfs the sweeper moved through are hit by the attack at the same time.
/// During the dash, the sweeper can go through mobs and tables. But not railings.
/// The first turf the sweeper moves into will be ignored for diagonal dashing because of how weird it feels to be hit in certain gameplay situations.
/// Any humans hit will heal the sweeper for var/dash_healing, and give it a stack of persistence.
/// Hitting a human will up the cooldown on the dash. Missing entirely means the dash comes off cooldown sooner.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/proc/SweepTheBackstreets(atom/prospective_fuel = target)
	if(stat == DEAD || !can_act)
		return FALSE
	if(dash_cooldown > world.time || dash_dashing || dash_preparing)
		return FALSE
	if(get_dist(src, prospective_fuel) > dash_range)
		return FALSE
	var/turf/dash_start_turf = get_turf(src)
	var/turf/dash_target_turf = get_ranged_target_turf_direct(src, prospective_fuel, dash_range)
	if(!dash_target_turf)
		return FALSE
	/// We got those checks out of the way - prepare to dash.
	dash_cooldown = world.time + dash_cooldown_time
	PrepareDash()
	LoseTarget()
	/// This section is for telegraphing the attack.
	face_atom(prospective_fuel)
	say("+2653 753 842396.+")
	var/obj/effect/temp_visual/sweeper_dash_warning/telegraph = new(get_turf(src))
	walk_towards(telegraph, dash_target_turf, 0.1 SECONDS)
	SLEEP_CHECK_DEATH(dash_windup)
	/// We're now dashing.
	BeginDash()
	walk_towards(src, dash_target_turf, dash_speed)
	SLEEP_CHECK_DEATH(get_dist(src, dash_target_turf) * dash_speed)

	/// This part is for some visual/audio feedback.
	var/datum/beam/really_temporary_beam = dash_start_turf.Beam(src, icon_state = "1-full", time = 3)
	really_temporary_beam.visuals.color = "#FE5343"
	playsound(src, 'sound/weapons/fixer/generic/knife3.ogg', 100, FALSE, 4)

	/// We're done dashing. Hit all the affected turfs at the same time (to avoid people dodging it by moving into it).
	/// Also, if we're dashing diagonally we're not hitting the first turf. Because it feels really weird from playtesting.
	/// I don't know a more elegant way to do it, but the following code will remove the first turf from the hit turfs list if we dashed diagonally.
	/// The alternative is that, for example, a player standing directly south of a sweeper dashing southwest will be hit by the dash. Which is really weird.
	var/moved_cardinals = FALSE
	var/direction_moved = get_dir(src, dash_start_turf)
	/// I know there's a define called ALL_CARDINALS but it's not a list and I don't really know how to use it.
	if(direction_moved == NORTH || direction_moved == SOUTH || direction_moved == WEST || direction_moved == EAST)
		moved_cardinals = TRUE
	if(!moved_cardinals)
		if(length(dash_hitlist_turfs) > 0)
			dash_hitlist_turfs -= dash_hitlist_turfs[1]
	/// The Sweeper won't have added the final turf onto its hit list, so we add it here.
	/// Yes it needs to get slept for 0.1 second here because... it hasn't finished moving or something. I've tested it. Trust me.
	SLEEP_CHECK_DEATH(0.1 SECONDS)
	CancelDash()
	walk(src, 0)
	dash_hitlist_turfs |= get_turf(src)
	SweepTheBackstreetsHit(dash_hitlist_turfs)
	/// Give the players a tiny bit of time to not instantly get auto hit by the sweeper after it dashes.
	SLEEP_CHECK_DEATH(0.4 SECONDS)
	/// We'll have them enter Evasive Mode after this dash.
	EvasiveMode()

	/// Re-target our old target.
	if(!client)
		GiveTarget(prospective_fuel)
	can_act = TRUE
	return TRUE


/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/proc/SweepTheBackstreetsHit(list/turfs)
	for(var/hit_turf in turfs)
		for(var/mob/living/hit_mob in HurtInTurf(hit_turf, dash_hitlist, melee_damage_upper * 1.5, melee_damage_type, check_faction = TRUE, hurt_mechs = TRUE, hurt_structure = TRUE, attack_type = (ATTACK_TYPE_MELEE | ATTACK_TYPE_SPECIAL)))
			to_chat(hit_mob, span_userdanger("The [src.name] viciously slashes you as it dashes past!"))
			SpawnAppropiateGibs(hit_mob)
			playsound(hit_mob, attack_sound, 100)
			// Big slice VFX
			var/obj/effect/temp_visual/slice/temp = new(hit_turf)
			temp.transform = temp.transform * 1.75
			temp.color = COLOR_MOSTLY_PURE_RED

			/// Dash will come off cooldown faster if it hits someone. Dodge it!
			dash_cooldown -= 4 SECONDS

			/// We gain persistence and heal if the target hit is human.
			if(istype(hit_mob, /mob/living/carbon/human))
				SweeperHealing(dash_healing)
				GainPersistence(1)

/// Called when we're entering a dash (passed all the checks).
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/proc/PrepareDash()
	dash_preparing = TRUE
	dash_dashing = FALSE
	/// Can't attack.
	can_act = FALSE
	/// Can't get pushed away during this.
	anchored = TRUE
	/// Reset our hit lists.
	dash_hitlist = list()
	dash_hitlist_turfs = list()

/// Called to begin dashing properly.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/proc/BeginDash()
	dash_preparing = FALSE
	/// All turfs we move into while dashing as long as this variable is TRUE will be registered by Move() to be passed onto SweepTheBackstreetsHit() by SweepTheBackstreets().
	dash_dashing = TRUE
	/// We can't attack.
	can_act = FALSE
	/// We can move again.
	anchored = FALSE
	/// We can move through mobs and tables.
	pass_flags = PASSMOB | PASSTABLE
	density = FALSE

/// This is called when the dash is cancelled early by a failed movement or when the dash reached its destination. It just resets us back to our base state.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/proc/CancelDash()
	dash_dashing = FALSE
	dash_preparing = FALSE
	pass_flags = initial(pass_flags)
	density = TRUE

/// Sweeper will sometimes enter Evasive Mode after a dash. Just a big mobility steroid and makes unpossessed sweepers move erratically - kind of like GWSS.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/proc/EvasiveMode()
	addtimer(CALLBACK(src, PROC_REF(DisableEvasiveMode)), dash_evasivemode_duration)
	if(!client)
		dodging = TRUE
		minimum_distance = 1
		retreat_distance = 2
		sidestep_per_cycle = 2
		ChangeMoveToDelay(dash_evasivemode_noclient_speed)
	/// Possessed sweepers get a smaller movement speed buff.
	else
		ChangeMoveToDelay(dash_evasivemode_client_speed)

/mob/living/simple_animal/hostile/ordeal/indigo_noon/lanky/proc/DisableEvasiveMode()
	dodging = initial(dodging)
	minimum_distance = initial(minimum_distance)
	retreat_distance = initial(retreat_distance)
	ChangeMoveToDelay(movespeed) // We do not use initial() here because it gets compiletime value, and we are going to apply nerfs on City modes on Initialize.
	sidestep_per_cycle = initial(sidestep_per_cycle)

/// I just want to make the telegraphing match properly, so we need a different duration for these than the normal 10 deciseconds
/obj/effect/temp_visual/sweeper_dash_warning
	name = "dash warning"
	desc = "Move aside!"
	icon = 'icons/effects/effects.dmi'
	icon_state = "tbird_bolt"
	color = COLOR_RED
	duration = 0.6 SECONDS
	movement_type = FLYING | PHASING

/// This subtype moves slower, attacks slower, deals a bit more damage per hit, and has access to an empowered lifesteal attack every once in a while after being hit.
/// Uses the chunky sweeper sprite made by insiteparaful.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky
	health = 750
	maxHealth = 750
	icon = 'ModularLobotomy/_Lobotomyicons/32x32.dmi'
	icon_state = "sweeper_limbus"
	icon_living = "sweeper_limbus"
	desc = "A humanoid creature wearing metallic armor. It has bloodied hooks in its hands.\nThis one has more bulk than its peers - it won't go down easy."
	/// They're slow.
	move_to_delay = 5
	/// These sweepers have a slower, but slightly stronger melee. Easier to parry if anything.
	rapid_melee = 0.8
	melee_damage_lower = 24
	melee_damage_upper = 26
	/// Holds the next moment when this sweeper can use Extract Fuel (lifesteal hit)
	var/extract_fuel_cooldown
	/// Holds the cooldown time between Extract Fuel uses
	var/extract_fuel_cooldown_time = 10 SECONDS
	/// Extract Fuel will hit for this much additional BLACK damage
	var/extract_fuel_extra_damage = 20
	/// Extract Fuel will heal the sweeper for this much health
	var/extract_fuel_healing = 125
	/// This controls whether the next hit actually sets off Extract Fuel's additional effects
	var/extract_fuel_active = FALSE
	/// We store the timer we use for cancelling Extract Fuel so we can delete it early if we've already used it
	var/extract_fuel_ongoing_timer
	/// If we've already used 333... 1973 before, we don't want to use it ever again
	var/used_last_stand = FALSE
	/// Amount of Persistence stacks gained when using 333... 1973.
	var/last_stand_stack_gain = 2

	// CoL balance adjustments. These will be applied on Initialize to rebalance this variant on City modes.
	// At the moment there are no changes, however there may be the need to nerf or buff them on City in the future.
	/// ADDS TO health and maxHealth. Positive values buff HP, negative values nerf it.
	var/COL_health_adjustment = 0
	/// ADDS TO Extract Fuel's cooldown time. Positive values make it take longer to recharge, negative ones shorten the recharge.
	var/COL_extractfuel_cooldown_adjustment = 0 SECONDS
	/// ADDS TO Extract Fuel's extra damage. Positive values make it hit harder, negative ones nerf it.
	var/COL_extractfuel_damage_adjustment = 0
	/// ADDS TO Last Stand's stack gain. Positive values makes it give more stacks, negative ones makes it give less.
	var/COL_laststand_stacks_adjustment = 1



/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky/Initialize()
	. = ..()
	/// I know this is weird but I don't know how to ONLY override Initialize() for indigo_noon without getting rid of the code from simple_animal and whatnot.
	/// I have to set these things here because normal indigo_noon initialization sets them.
	icon_living = "sweeper_limbus"
	icon_state = icon_living
	attack_sound = 'sound/effects/ordeals/indigo/stab_1.ogg'

	/// COL Rebalancing
	if(SSmaptype.maptype in SSmaptype.citymaps)
		maxHealth += COL_health_adjustment
		health += COL_health_adjustment
		extract_fuel_cooldown_time += COL_extractfuel_cooldown_adjustment
		extract_fuel_extra_damage += COL_extractfuel_damage_adjustment
		last_stand_stack_gain += COL_laststand_stacks_adjustment

/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky/PostDamageReaction(damage_amount, damage_type, source, attack_type)
	. = ..()
	if(!used_last_stand && health <= maxHealth * 0.40 && prob(60))
		LastStand()
		return
	if(extract_fuel_cooldown <= world.time && prob(60) && (get_dist(source, src) < 3))
		PrepareExtractFuel()
		return

/// This ability is basically "333... 1973". It gives the chunky sweeper 3 persistence stacks, that's all.
/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky/proc/LastStand()
	if(stat == DEAD)
		return FALSE
	used_last_stand = TRUE
	say("+333... 1973.+")
	GainPersistence(last_stand_stack_gain)

/// The following few code chunks are dedicated to the Extract Fuel mechanic specific to this sweeper type. Basically, it's a lifesteal hit they can use every once in a bit.
/// When the sweeper takes a hit, if it's off cooldown, it'll buff itself for its next hit and warn the player, giving them a brief grace period to disengage or prepare.
/// If they don't get away in time, they'll be hit by an empowered attack that gives persistence, heals the sweeper for a good chunk and spawns some gibs as VFX.
/// The buff goes away in 2.5 seconds or after landing the hit.
/// Extract Fuel doesn't get activated by ranged hits, but like, even if it did, it'd be useless. These things are REALLY slow and easy to kite.
/// But I don't have any intention of giving them some sort of countermeasure, it's just a Noon Ordeal...

/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky/AttackingTarget(atom/attacked_target)
	. = ..()
	if(. && extract_fuel_active && istype(attacked_target, /mob/living))
		var/mob/living/victim = attacked_target
		CancelExtractFuel(TRUE)
		SpawnAppropiateGibs(attacked_target)
		visible_message(span_danger("The [src.name] tears into [victim.name] and refuels itself with some of [victim.p_their()] viscera!"))
		SweeperHealing(extract_fuel_healing)
		if(ishuman(attacked_target))
			GainPersistence(1)


/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky/proc/PrepareExtractFuel()
	/// I have no idea what could cause this, but just in case
	if(extract_fuel_active)
		return FALSE
	if(stat >= DEAD)
		return FALSE
	/// Go on cooldown.
	extract_fuel_cooldown = world.time + extract_fuel_cooldown_time
	/// Warn the players so they can back off or get ready to parry.
	say("+38725 619.+")
	animate(src, 2 SECONDS, color = "#FE5343")
	visible_message(span_danger("The [src.name] winds up for a devastating blow!"), span_info("You prepare to extract fuel from your victim."))
	/// We're gonna sleep them because otherwise someone could hit the sweeper the DECISECOND before it's gonna attack and get slapped by a huge hit
	/// This gives them enough margin to run away or parry
	SLEEP_CHECK_DEATH(0.6 SECONDS)
	/// Make our attack scary.
	melee_damage_lower += extract_fuel_extra_damage
	melee_damage_upper += extract_fuel_extra_damage
	attack_sound = 'sound/weapons/fixer/generic/finisher1.ogg'
	extract_fuel_active = TRUE
	/// If we haven't landed the hit in the following few seconds, we will lose the buff.
	extract_fuel_ongoing_timer = addtimer(CALLBACK(src, PROC_REF(CancelExtractFuel), FALSE), 2.6 SECONDS, TIMER_STOPPABLE)

/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky/proc/CancelExtractFuel(early)
	/// Timer cleanup
	ExtractFuelTimerCleanup()
	/// We go back to normal.
	melee_damage_lower = initial(melee_damage_lower)
	melee_damage_upper = initial(melee_damage_upper)
	attack_sound = 'sound/effects/ordeals/indigo/stab_1.ogg'
	extract_fuel_active = FALSE
	animate(src, 0.5 SECONDS, color = initial(color))
	if(!early)
		visible_message(span_danger("The [src.name] lowers its aggressive stance."), span_info("You give up on the fuel extraction attempt."))
		for(var/mob/living/carbon/human/viewer in viewers(7, src))
			balloon_alert(viewer, "The [src.name] lowers its aggresive stance.")


/// This cleanup exists because if we land a hit with Extract Fuel, we want to turn it off, but there's still an ongoing timer it will call CancelExtractFuel
/mob/living/simple_animal/hostile/ordeal/indigo_noon/chunky/proc/ExtractFuelTimerCleanup()
	if(extract_fuel_ongoing_timer)
		deltimer(extract_fuel_ongoing_timer)
		extract_fuel_ongoing_timer = null

